# List available recipes
default:
  @just --list

alias b := build
alias t := test
alias l := lint

# Run cargo doc and open result in browser
[group('build')]
doc:
  cargo doc --all-features --no-deps --open

# Run cargo build
[group('build')]
build:
  cargo build --release

# Run cargo clean
[group('build')]
clean:
  cargo clean

# Install cargo tools used in package maintenance
[group('build')]
install_dev_tools:
  echo "Installing optional tools for depelopment."
  cargo install --locked release-plz
  cargo install --locked cargo-audit
  cargo install --locked cargo-outdated
  cargo install --locked cargo-llvm-cov
  cargo install --locked cargo-expand

# Format source code with cargo fmt
[group('lint')]
fmt:
  cargo +nightly fmt --all

# Lint source code CI linter
[group('lint')]
lint:
  cargo check --release
  cargo clippy --release --lib --all-features -- -D warnings

# Lint source code with strict linter
[group('lint')]
pedantic:
  cargo clippy --release -- -W clippy::pedantic

# Run cargo audit to vet dependencies
[group('lint')]
audit:
  cargo audit
# Run cargo audit to vet dependencies
[group('lint')]
outdated:
  cargo outdated

set positional-arguments
# Run tests for all features
[group('test')]
test args='':
  cargo test --all-features $1 -- --show-output

# Run llvm-cov code coverage tool and open report in browser
[group('test')]
cov:
  cargo llvm-cov --open

# Run same testing commands as on CI server
[group('test')]
ci:
  cargo clippy --release --lib --all-features -- -D warnings
  cargo build --release
  cargo test --release --all-features
  cargo test --release --examples
